plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

allprojects {
    repositories {
        maven {
            name "maven-public"
            url nexusUrl
            allowInsecureProtocol url.getScheme() == "http"
            if (project.hasProperty("nexusUser") && project.hasProperty("nexusUser")) {
                credentials {
                    username nexusUser
                    password nexusPassword
                }
            }
        }
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'ru.iteco'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }
}